dnl Initialise autoconf

AC_INIT(app/equator.cpp)
AM_CONFIG_HEADER(config.h)

dnl Initialise automake

AM_INIT_AUTOMAKE(equator, 0.0.2)

dnl check if user wishes maintainer-mode ("--enable-maintainer-mode = yes")

AM_MAINTAINER_MODE

dnl Allow the program target name to be overriden

AC_ARG_PROGRAM

dnl Test for C++ compiler

AC_PROG_CXX

AC_ARG_ENABLE(debug,
    [  --enable-debug          enable debug information [default=no]],
    [
        if test "$enableval" = "yes"; then
            CXXFLAGS="$CXXFLAGS -Wall -DDEBUG"
        else
            CXXFLAGS="$CXXFLAGS -Wall -DNDEBUG"
        fi
    ],[
        CXXFLAGS="$CXXFLAGS -Wall -DNDEBUG"
    ]
)

AC_LANG_CPLUSPLUS

SDL_VERSION=1.2.0
AM_PATH_SDL($SDL_VERSION)
CPPFLAGS="$CPPFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

PKG_CHECK_MODULES(WF, wfmath-0.2 >= 0.2.10 skstream-0.2 >= 0.2.4 \
                      atlascpp-0.4 >= 0.4.5 mercator-0.2 eris-1.0 \
                      coal-0.4 coal-blade-0.4 coal-iso-0.4 \
                          ,
    [
        CPPFLAGS="$CPPFLAGS $WF_CFLAGS"
        LIBS="$LIBS $WF_LIBS"
    ],
    [
        AC_MSG_ERROR(Couldn't find WF libs.)
    ])

AC_CHECK_LIB(SDL_image,IMG_Load)
AC_CHECK_LIB(GL,glViewport)
AC_CHECK_LIB(GLU,gluPerspective)

dnl Versions required

PKG_CHECK_MODULES(GTK, gtkglextmm-1.0 ,
    [
        CPPFLAGS="$CPPFLAGS $GTK_CFLAGS"
        LIBS="$LIBS $GTK_LIBS"
    ],
    [
        AC_MSG_ERROR(Couldn't find GTK libs.)
    ])

AC_MSG_CHECKING([for round in libm])
AC_TRY_COMPILE([
  #include <cmath>
],[
  round(0.1);
],[
  AC_MSG_RESULT(yes)
],[
    AC_MSG_RESULT(no)
    AC_MSG_CHECKING([for round in libm with _ISOC99_SOURCE])

    CXXFLAGS="$CXXFLAGS -D_ISOC99_SOURCE"

    AC_TRY_COMPILE([
      #include <math.h>
    ],[
      round(0.1);
    ],[
      AC_MSG_RESULT(yes)
    ],[
      AC_MSG_RESULT(no)
    ])

])


dnl Generate files
AC_OUTPUT([
	Makefile
	app/Makefile
])
